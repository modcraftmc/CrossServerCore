plugins{
    id 'com.github.johnrengelman.shadow' version '7.+'
    id 'maven-publish'
}

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'

configurations {
    // Create the library configuration, where our non-mod libraries will be added to
    library
    // Any dependency in the library configuration will also be added to the implementation configuration
    implementation.extendsFrom library

    shade
    library.extendsFrom shade
}

repositories {
    mavenCentral()
    maven {
        name = 'papermc'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
}

dependencies {
    compileOnly 'com.velocitypowered:velocity-api:3.3.0-SNAPSHOT'
    annotationProcessor 'com.velocitypowered:velocity-api:3.3.0-SNAPSHOT'
    shade(group: 'com.rabbitmq', name: 'amqp-client', version: '[5.16.0]')
}

shadowJar {
    configurations = [project.configurations.shade]
    classifier = ''

    //relocate 'org.mongodb.mongodb-driver-sync', 'shaded.mongodb-driver-sync'
    //relocate 'com.moandjiezana.toml', 'shaded.toml'

    archiveBaseName = "${project.name}"

    if(project.release_type == "snapshot") {
        archiveVersion = "${plugin_version}-SNAPSHOT"
    } else{
        archiveVersion = "${plugin_version}"
    }
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allJava
}

artifacts {
    archives sourcesJar
}

jar.finalizedBy('shadowJar')

test {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            name = "ModcraftMaven"
            if(project.release_type == "snapshot") {
                url = "https://maven.modcraftmc.fr/snapshots"
            } else{
                url = "https://maven.modcraftmc.fr/releases"
            }
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        crossServerCore(MavenPublication) { publication ->
            project.shadow.component(publication)

            groupId = "fr.modcraftmc"
            artifactId = "cross-server-core-proxy-extension"
            version = project.plugin_version

            artifact sourcesJar{
                classifier('sources')
            }
        }
    }
}