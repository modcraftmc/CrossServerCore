plugins{
    id 'com.github.johnrengelman.shadow' version '7.+'
}

apply plugin: 'com.github.johnrengelman.shadow'

configurations {
    // Create the library configuration, where our non-mod libraries will be added to
    library
    // Any dependency in the library configuration will also be added to the implementation configuration
    implementation.extendsFrom library

    shade
    library.extendsFrom shade
}

dependencies {
    shade(group: 'com.rabbitmq', name: 'amqp-client', version: '[5.16.0]')
    shade(group: 'org.mongodb', name: 'mongodb-driver-sync', version: '[4.9.0]')
    shade(group: 'com.moandjiezana.toml', name: 'toml4j', version: '[0.7.2]')
}

shadowJar {
    configurations = [project.configurations.shade]
    classifier = ''

    //relocate 'org.mongodb.mongodb-driver-sync', 'shaded.mongodb-driver-sync'
    //relocate 'com.moandjiezana.toml', 'shaded.toml'
    dependencies {
        exclude(dependency('org.slf4j:slf4j-api'))
        exclude(dependency('com.google.code.gson:gson'))
    }

    archiveBaseName = "${project.name}"
    archiveVersion = "${mc_version}-${mod_version}"
}

jar.finalizedBy('shadowJar')


publishing {
    repositories {
        maven {
            name = "ModcraftMaven"
            url = "https://maven.modcraftmc.fr/releases"
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) { publication ->
            project.shadow.component(publication)
            groupId = "fr.modcraftmc"
            artifactId = "cross-server-core"
            version = project.mod_version
        }
    }
}